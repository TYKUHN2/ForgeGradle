plugins {
    id 'net.minecrell.licenser' version '0.4.1'
    id 'org.ajoberstar.grgit' version '3.0.0'
    id 'com.github.ben-manes.versions' version '0.20.0'
  //id 'com.github.johnrengelman.shadow' version '2.0.4'
}

apply plugin: 'java-library'
apply plugin: 'eclipse'

sourceCompatibility = targetCompatibility = '11' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '11'
}

group = 'net.minecraftforge.gradle'
version = gitVersion()
def gitVersion() {
    def raw = grgit.describe(longDescr: true, tags: true)
    def desc = (raw == null ? 'unknown-unknown-unknown' : raw).split('-') as List
    def hash = desc.remove(desc.size() - 1)
    def offset = desc.remove(desc.size() - 1)
    def tag = desc.join('-')
    def branch = grgit.branch.current().name    
    return "${tag}.${offset}" //${t -> if (branch != 'master') t << '-' + branch}"
}

sourceSets {
    common
    mcp
    patcher
    userdev
}

configurations {
    all {
        exclude group: 'org.ow2.asm'
    }
}

jar {
    from sourceSets.common.output
    from sourceSets.mcp.output
    from sourceSets.patcher.output
    from sourceSets.userdev.output
}

repositories {
    jcenter()
    maven { url = 'https://files.minecraftforge.net/maven' }
}

license {
    header = file('HEADER')
    ext {
        name = 'ForgeGradle'
        year = 2018
        fullname = 'Forge Development LLC'
    }
    exclude '**/*.properties'
    exclude 'net/minecraftforge/gradle/common/diff/'
    exclude 'net/minecraftforge/gradle/common/util/JavaVersionParser.java'
}

wrapper {
    gradleVersion = '5.6.4'
    distributionType = Wrapper.DistributionType.ALL
}

dependencies {
    implementation gradleApi()
    
    implementation 'com.cloudbees:diff4j:1.2'
    implementation 'commons-io:commons-io:2.4'
    implementation 'com.github.jponge:lzma-java:1.3' // replaces the LZMA binary
    implementation 'com.nothome:javaxdelta:2.0.1' // GDIFF implementation for BinPatches
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.google.guava:guava:26.0-jre'
    implementation 'de.siegmar:fastcsv:1.0.2'
    implementation 'net.minecraftforge:artifactural:1.0.+'
    implementation 'org.apache.maven:maven-artifact:3.6.0'
    implementation 'org.apache.httpcomponents:httpclient:4.3.3'

    implementation sourceSets.common.output
    implementation sourceSets.mcp.output
    implementation sourceSets.common.output
    implementation sourceSets.mcp.output
    implementation sourceSets.common.output
}

//Gradle doesn't add it's own source when doing the API. So lets hack it in!
import org.gradle.plugins.ide.eclipse.model.*
import org.gradle.plugins.ide.eclipse.model.internal.*
project.extensions.eclipse.classpath.file.whenMerged { Classpath cp ->
    def gradleSrc = gradle.gradleHomeDir.absolutePath.replace(File.separator, '/') + '/src/'
    cp.entries.each { entry ->
        if ((entry in AbstractLibrary) && entry.library.file.name.startsWith('gradle-')) {
            def type = (entry.library.file.name =~ "^gradle(-(.*))?-(${gradle.gradleVersion})")[0][2]
            if (type == 'api') type = 'core-api' //Gradle name is different for cores
            if (type == '') type = 'core'
            entry.sourcePath = new FileReferenceFactory().fromPath(gradleSrc + type)
        }
    }
}
